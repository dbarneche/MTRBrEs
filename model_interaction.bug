model {
	
	# priors for fixed effects in metabolic rate analysis
	for (i in 1:K) { 
		beta[i] ~ dnorm(0, 0.0001)
	}
	tauB  ~   dgamma(1.0E-3,1.0E-3)
	varB  <-  1/tauB # variance, equivalent to inverse(tauB)

	# priors for random effects at the run level
	for(i in 1:max(run)) {
		r[i] ~ dnorm(0, tauR)
	}
	tauR  ~   dgamma(1.0E-3,1.0E-3)
	varR  <-  1/tauR # variance, equivalent to inverse(tauR)

	# priors for random effects at the plate level
	for(i in 1:max(plate)) {
		p[i] ~ dnorm(0, tauP)
	}
	tauP  ~   dgamma(1.0E-3,1.0E-3)
	varP  <-  1/tauP # variance, equivalent to inverse(tauP)

	#linear regression for metabolic rate
	for(i in 1:length(lnRate)) {
		lnRate[i]  ~  dnorm(muB[i], tauB)
      	muB[i]     <- r[run[i]] + p[plate[i]] + inprod(beta[], jagsModelMatrix[i,]) 
	}
}
